# Correction of slab/slice profile

Here we will show how to correct the error generated by the slice/slab profile used by the excitation pulse.

## Corrupted MP2RAGE image

```@example slab
using QuantitativeMRI
using JLD2, CairoMakie
d = load(joinpath(pwd(),"../..","data","corrupted.jld2"))

MP2RAGE = d["MP2RAGE"]
T1map = d["T1map"]
p_MP2 = d["params_MP2RAGE"]
angle_profile = d["angle_profile"]
TI1 = d["im_reco"][:,:,1]
TI2 = d["im_reco"][:,:,2]

f=Figure()
ax = Axis(f[1,1], title="Corrupted MP2RAGE image",xlabel="phase encoding direction", ylabel="partition encoding direction")
heatmap!(ax,MP2RAGE,colormap=:grays)
hidedecorations!(ax,label=false)
f
```

You can see that the image is corrupted by the slab profile of the excitation pulse along the Y axis on the image corresponding to the **partition encoding direction**.

This is due to a wrong choice of excitation pulse, here a hermite shape. We can take a look at the corresponding excitation profile:

```@example slab
f=Figure()
ax = Axis(f[1,1], title="Corrupted MP2RAGE image",xlabel="partition encoding direction", ylabel="effective flip angle [degrees]")
lines!(ax,angle_profile)
f
```

## Fix the T1 map
Hopefully, if we know the effective flip angle profile, we can correct the resulting T1 map.


```@example slab
p = deepcopy(p_MP2)

T1corr = similar(MP2RAGE)
T1,range_T1,LUT = mp2rage_T1maps(MP2RAGE,p_MP2)

LUT_vec = []
range_T1_vec = []
for i=1:size(MP2RAGE,2)
  p.α₁ = d["angle_profile"][i]
  p.α₂ = d["angle_profile"][i] ./ p_MP2.α₁ .* p_MP2.α₂

  T1corr[:,i], range_T1, LUT= mp2rage_T1maps(MP2RAGE[:,i],p)
  push!(LUT_vec,LUT)
  push!(range_T1_vec,range_T1)
end

f=Figure()
ax = Axis(f[1,1], title="Initial T1 map")
heatmap!(ax,T1,colorrange = (800,2000))
hidedecorations!(ax)
ax = Axis(f[1,2], title="Corrected T1 map")
h=heatmap!(ax,T1corr,colorrange = (800,2000))
hidedecorations!(ax)
Colorbar(f[1,3], h, label="T1 [ms]")
f
```

The idea is to compute different lookup table (LUT) for each partition position, using the effective flip angle profile. This allows us to correct the T1 map for the slab profile effect.

Let's take a look at 2 differents LUTs :

```@example slab
f=Figure()
ax = Axis(f[1,1], title="LUT for partition position 1 and 96")
xlims!(ax,0,range_T1_vec[96][end])
ylims!(ax,-0.5,0.5)

lines!(ax,range_T1_vec[1],LUT_vec[1],label="angle = $(angle_profile[1])",color=:blue)
lines!(ax,range_T1_vec[96],LUT_vec[96],label="angle = $(angle_profile[96])",color=:green)

val_MP2 = 0.1
idxFirst1 = searchsortedfirst(LUT_vec[1], val_MP2,lt= >=)
idxFirst2 = searchsortedfirst(LUT_vec[96], val_MP2,lt= >=)
hlines!(ax,val_MP2,xmax=range_T1_vec[96][idxFirst2]/range_T1_vec[96][end],color=:red,linestyle=:dot)

vlines!(ax,range_T1_vec[1][idxFirst1],color=:blue,linestyle=:dash)
vlines!(ax,range_T1_vec[96][idxFirst2],color=:green,linestyle=:dash)
f
```

You can see that the different effective flip angle profiles lead to different LUTs, which allows us to correct the T1 map for the slab profile effect.

## Correcting back the MP2RAGE and TI images

We can also correct the MP2RAGE image using the corrected T1 map.

```@example slab
res = QuantitativeMRI.T1maps_mp2rage(T1corr,p_MP2) 


begin
  sl=[:,:]
  f = Figure()

  ax=Axis(f[1,1],title="Initial MP2RAGE")
  h=heatmap!(ax,MP2RAGE,colormap=:grays)

  ax=Axis(f[1,2],title="corrected MP2RAGE")
  h=heatmap!(ax,res[1], colormap=:grays)

  ax=Axis(f[2,1],title="Initial TI₁")
  h=heatmap!(ax,abs.(TI1),colormap=:grays)

  ax=Axis(f[2,2],title="TI₁ corrected")
  h=heatmap!(ax,res[2],colormap=:grays)

  ax=Axis(f[3,1],title="Initial TI₂")
  h=heatmap!(ax,abs.(TI2),colormap=:grays)

  ax=Axis(f[3,2],title="TI₂ corrected")
  h=heatmap!(ax,res[3],colormap=:grays)


  for ax in f.content   # hide decoration befor adding colorbar
    hidedecorations!(ax)
  end

  Colorbar(f[2,3],h,label = "T₁ [ms]", flip_vertical_label=true)
  f
end
```

As you can see, the corrected MP2RAGE image is now free of the slab profile effect. 

However, the generated TI₁ and TI₂ images are not close to the original ones, as they contains salt-and-pepper noise in the background but also some differences occurs for tissues with T1 not landing in the range of the LUT. Here, it is the case for the ventricular blood.
